plugins {
    id("xyz.jpenilla.run-paper") version "2.1.0"
    id("craftinghomes.java")
}

sourceSets {
    craftinghomes {
        java {
            srcDir("src/main/java")
        }
    }
}

configurations {
    craftinghomesShadow.extendsFrom(shadow)
}

repositories {
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT")
    compileOnly 'org.jetbrains:annotations:24.0.1'

    craftinghomesShadow(project(":craftinghomes"))
    craftinghomesShadow('org.apache.commons:commons-io:1.3.2')
    shadow(project(":common"))
}

tasks {
    build {
        dependsOn("buildCraftinghomes")
    }
    runServer {
        dependsOn("build")
    }
    runPaper {
        disablePluginJarDetection()
    }
}

tasks.register('buildCraftinghomes', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveFileName.set("CraftingHomes-bukkit-v${version}-1.8.8-${findProperty('latestMinecraftVersion')}.jar")
    destinationDirectory = file("$rootDir/build")

    description = "Build craftinghomes"
    group = "build"
    from sourceSets.main.output
    configurations = [project.configurations.craftinghomesShadow, project.configurations.shadow]
}

// https://papermc.io/downloads/all
def supportedVersions = ["1.20.2", "1.20.1", "1.20",
                         "1.19.4", "1.19.3", "1.19.2", "1.19.1", "1.19",
                         "1.18.2", "1.18.1", "1.18",
                         "1.17.1", "1.17",
                         "1.16.5", "1.16.4", "1.16.3", "1.16.2", "1.16.1",
                         "1.15.2", // "1.15.1", "1.15", not supported here, because nogui argument is not existing there
                         "1.14.4", "1.14.3", "1.14.2", "1.14.1", "1.14",
                         "1.13.2", "1.13", "1.13.1", "1.13-pre7",
                         "1.12.2", "1.12.1", "1.12",
                         "1.11.2",
                         "1.10.2",
                         "1.9.4",
                         "1.8.8"]

supportedVersions.forEach { version ->
    tasks.register("runServer-$version", xyz.jpenilla.runpaper.task.RunServer) {
        dependsOn("buildCraftinghomes")
        minecraftVersion(version)
        if (version.startsWith("1.13") || version.startsWith("1.14")) {
            javaLauncher.set(javaToolchains.launcherFor {
                languageVersion.set(JavaLanguageVersion.of(11))
            })
        }
        runDirectory.set(file("run/$version"))
        jvmArgs("-DPaper.IgnoreJavaVersion=true", "-Dcom.mojang.eula.agree=true")
        group = "run paper"
        pluginJars = files("$rootDir/build/CraftingHomes-bukkit-v${project.version}-1.8.8-${findProperty('latestMinecraftVersion') as String}.jar")
    }
}