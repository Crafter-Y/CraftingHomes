plugins {
    id("xyz.jpenilla.run-paper") version "2.1.0"
    id("craftinghomes.java")
}

sourceSets {
    craftinghomes {
        java {
            srcDir("src/main/java")
        }
    }
}

configurations {
    craftinghomesShadow.extendsFrom(shadow)
}

repositories {
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT")

    craftinghomesShadow(project(":craftinghomes"))
    shadow(project(":common"))
}

tasks {
    build {
        dependsOn("buildCraftinghomes")
    }
    runServer {
        dependsOn("build")
    }
    runPaper {
        disablePluginJarDetection()
    }
}

tasks.register('buildCraftinghomes', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveFileName.set("CraftingHomes-bukkit-v${version}-1.8.8-${findProperty('latestMinecraftVersion')}.jar")
    destinationDirectory = file("$rootDir/build")

    description = "Build craftinghomes"
    group = "build"
    from sourceSets.main.output
    configurations = [project.configurations.craftinghomesShadow, project.configurations.shadow]
}

runServer {
    dependsOn("buildCraftinghomes")
    minecraftVersion(findProperty('runPaperMinecraftVersion') as String)
    pluginJars = files("$rootDir/build/CraftingHomes-bukkit-v${project.version}-1.8.8-${findProperty('latestMinecraftVersion') as String}.jar")
}